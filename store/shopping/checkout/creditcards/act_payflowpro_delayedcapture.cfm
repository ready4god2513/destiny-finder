
<!--- CFWebstore®, version 6.43 --->

<!--- CFWebstore® is ©Copyright 1998-2009 by Dogpatch Software, All Rights Reserved. This code may not be copied or sold without permission of the original author. Dogpatch Software may be contacted at info@cfwebstore.com --->

<!--- This template is used to complete the payment processing on an order using PayFlow Pro. The order must have been previously authorized. It looks up the authorization number and bills.  Called from shopping\admin\act_billing.cfm

	Required: Order_no
	
--->


<!--- Retrieve Verisign PayFlowPro settings --->
<cfquery name="GetSettings" datasource="#Request.DS#" username="#Request.user#" password="#Request.pass#" maxrows="1">
SELECT * FROM #Request.DB_Prefix#CCProcess
</cfquery>

<!--- Look up the Order Info --->
<cfquery name="GetOrderTx" datasource="#Request.DS#" username="#Request.user#" password="#Request.pass#" maxrows="1">
SELECT Order_No, TransactNum, OrderTotal, Notes
FROM #Request.DB_Prefix#Order_No
WHERE Order_No = <cfqueryparam value="#attributes.Order_no#" cfsqltype="CF_SQL_INTEGER">
AND Paid = 0
AND Void = 0
</cfquery>


<!--- if record found, process the payment --->
<cfif GetOrderTx.recordcount is 1 AND GetOrderTx.TransactNum is not "">

	<cflock name="payflowpro" timeout="150" type="EXCLUSIVE">

		<CFX_PAYFLOWPRO 
	 			QUERY			= "RESULT"
                HostAddress		= "#GetSettings.CCServer#" 
				HostPort		= "#GetSettings.Setting1#" 
				TimeOut			= "30" 
				TRXTYPE			= "D"
                TENDER         	= "C"
                PARTNER       	= "#GetSettings.Setting2#"
                USER			= "#GetSettings.Username#"
     			PWD 			= "#GetSettings.Password#"
				AMT				="#NumberFormat(GetOrderTx.OrderTotal, "________.00")#"
				ORIGID			= "#GetOrderTx.TransactNum#"
				CERTPATH 		= "#GetSettings.Setting3#" 
                ACCT			= ""
     			EXPDATE			= ""
				CVV2			= ""
				STREET			= ""
				ZIP				= ""
				NAME			= ""
                INVNUM			= ""
				CUSTID			= ""
				COMMENT1       	= "This transaction was automatically generated by cfwebstore"
                COMMENT2       	= ""
				DEBUGMODE		= "0"
				PARMLIST		= "" 
				PROXYADDRESS	= ""
				PROXYPORT		= ""
				PROXYLOGON		= ""
				PROXYPASSWORD	= ""
				>
	 
	</cflock>

	<!--- Debug --->
	<!--- <cfdump var="#Result#"> --->
	<cfset thedate = dateformat(now(),"mm/dd/yy")>

	<!---If the message is approved, continue processing the order---->
	<cfif result.result IS "0">
  
  		<cfquery name="OrderPaid" datasource="#Request.DS#" username="#Request.user#" password="#Request.pass#">	
			UPDATE #Request.DB_Prefix#Order_No
			SET Paid = 1,
			Notes = 'Card billed #CurrencyFormat(GetOrderTx.OrderTotal)# on #thedate#. #GetOrderTx.Notes#',
			AuthNumber = <cfqueryparam cfsqltype="CF_SQL_VARCHAR" value="#result.authcode#'">,
			TransactNum = <cfqueryparam cfsqltype="CF_SQL_VARCHAR" value="#result.PNREF#">,
			Admin_Name = <cfqueryparam cfsqltype="CF_SQL_VARCHAR" value="#Session.Realname#">,
			Admin_Updated = <cfqueryparam cfsqltype="CF_SQL_TIMESTAMP" value="#Now()#">
			WHERE Order_No = <cfqueryparam cfsqltype="CF_SQL_INTEGER" value="#attributes.Order_no#">
		</cfquery>
		
	<cfelse>
  		<cfset ErrorMessage = result.respmsg>
  		
		<cfquery name="OrderPaid" datasource="#Request.DS#" username="#Request.user#" password="#Request.pass#">	
			UPDATE #Request.DB_Prefix#Order_No
			SET 
			Notes = 'CC billing failed on #thedate#: #result.respmsg#  #GetOrderTx.Notes#',
			Admin_Name = <cfqueryparam cfsqltype="CF_SQL_VARCHAR" value="#Session.Realname#">,
			Admin_Updated = <cfqueryparam cfsqltype="CF_SQL_TIMESTAMP" value="#Now()#">
			WHERE Order_No = <cfqueryparam cfsqltype="CF_SQL_INTEGER" value="#attributes.Order_no#">
		</cfquery>
			
	</cfif>
			
</cfif>
