
<!--- CFWebstore®, version 6.43 --->

<!--- CFWebstore® is ©Copyright 1998-2009 by Dogpatch Software, All Rights Reserved. This code may not be copied or sold without permission of the original author. Dogpatch Software may be contacted at info@cfwebstore.com --->

<!--- if UserSettings.use_ccard is turned on, this template runs a $1 authorization only on a credit card to see if it is active.

User.CardisValid is set to 1 when user is first added AND after every successful credit card billing.
	Called by act_Register.cfm during registration process 
	Called from users/manager/act_ccard_update.cfm when user updates cc info
	Can be called directly from user admin form to test card.
--->

<!--- These variables are set during card processing --->	
<cfset setactive = 0>
<cfset amount = 0>
<cfset adminnote = "">
<cfset token = "">

<cfinclude template="../shopping/qry_get_order_settings.cfm">

<!--- Check user permissions. If not admin, only allow access to user's own record --->
<cfmodule template="../access/secure.cfm"
	keyname="users"
	requiredPermission="4"
	>	
	
<cfif NOT ispermitted>
	<cfset attributes.UID = Session.User_ID>
</cfif>

<cfparam name="attributes.UID" default="#Session.User_ID#">

<!--- CUSTOM CODE ----->
<cfif not isDefined('qry_get_user.NameOnCard')>
	<cfinclude template="qry_get_user.cfm">
</cfif>


<cfif len(qry_get_user.NameonCard) and len(qry_get_user.CardNumber) and isDate(qry_get_user.cardexpire)>

	<cfquery name="GetSettings" datasource="#Request.DS#" username="#Request.user#" password="#Request.pass#" maxrows="1">
		SELECT * FROM #Request.DB_Prefix#CCProcess
	</cfquery>

	<cfset NameLen = ListLen(qry_get_user.NameonCard, " ")>
	<cfset LastName = ListGetAt(qry_get_user.NameonCard, NameLen, " ")>
	<cfset FirstName = ListDeleteAt(qry_get_user.NameonCard, NameLen, " ")>
	
	<cfset expiration = "#Dateformat(qry_get_user.cardexpire,'MM')#" & "#Dateformat(qry_get_user.cardexpire,'YY')#">
	
	<!--- Decrypt CC --->
	<cfmodule template="../customtags/crypt.cfm" string="#qry_get_user.EncryptedCard#" key="#Request.encrypt_key#" action="de">

	<!--- INSERT CC BILLING INFO HERE --->

	<cfif get_Order_Settings.CCProcess IS "PayfloPro">

		<!---- PAYFLOW PRO PROCESSING CODE ------------------->
		<cflock name="payflowpro" timeout="150" type="EXCLUSIVE">
			<CFX_PAYFLOWPRO 
					QUERY			= "RESULT"
					HostAddress		="#GetSettings.CCServer#" 
					HostPort		="#GetSettings.Setting1#" 
					TimeOut			="30" 
					TRXTYPE			="A"
					TENDER         	= "C"
					PARTNER       	= "INNO"
					USER			="#GetSettings.Username#"
					PWD 			="#GetSettings.Password#"
					ACCT			="#crypt.value#"
					EXPDATE			="#expiration#"
					CVV2			=""
					CERTPATH 		="#GetSettings.Setting3#" 
					STREET			=""
					ZIP				="#qry_get_user.cardzip#"
					NAME			="#qry_get_user.NameonCard#"
					AMT				="1.00"
					INVNUM			="U#attributes.UID#T#dateformat(now(),'yyyymmddhhmmss')#"
					CUSTID			="#qry_get_user.user_ID#"
					COMMENT1       	= "auto authorization"
					COMMENT2       	= ""
					DEBUGMODE		= "0"
					PARMLIST		= "" 
					PROXYADDRESS	= ""
					PROXYPORT		= ""
					PROXYLOGON		= ""
					PROXYPASSWORD	= ""
			>
		</cflock>
	
		<!--- Debug 
		<cfdump var="#Result#"> --->
		
		<cfif result.result IS 0>
			<cfset setactive = 1>
			<cfset adminnote = "#dateformat(now(),"mm/dd/yy")#: card authorized #result.authcode#. ">
			<!--- Send through a void for this transaction --->
			<CFX_PAYFLOWPRO 
	 			QUERY			= "RESULT"
                HostAddress		= "#GetSettings.CCServer#" 
				HostPort		= "#GetSettings.Setting1#" 
				TimeOut			= "30" 
				TRXTYPE			= "V"
                TENDER         	= "C"
                PARTNER       	= "#GetSettings.Setting2#"
                USER			= "#GetSettings.Username#"
     			PWD 			= "#GetSettings.Password#"
				AMT				="1.00"
				ORIGID			= "#result.PNREF#"
				CERTPATH 		= "#GetSettings.Setting3#" 
                ACCT			= ""
     			EXPDATE			= ""
				CVV2			= ""
				STREET			= ""
				ZIP				= ""
				NAME			= ""
                INVNUM			= ""
				CUSTID			= ""
				COMMENT1       	= "This transaction was automatically generated by cfwebstore"
                COMMENT2       	= ""
				DEBUGMODE		= "0"
				PARMLIST		= "" 
				PROXYADDRESS	= ""
				PROXYPORT		= ""
				PROXYLOGON		= ""
				PROXYPASSWORD	= ""
				>
		<cfelse>
			<cfset adminnote = "#dateformat(now(),"mm/dd/yy")#: card not authorized #result.respmsg#">		
		</cfif>
	
	<cfelseif get_Order_Settings.CCProcess IS "AuthorizeNet3">
		<!---- AUTHORIZE.NET CREDIT CARD BILLING CODE ----------->	
		<cfmodule template="../shopping/checkout/creditcards/authorizenet30.cfm"
		QUERYNAME="Results"
		LOGIN="#GetSettings.Username#"
		PASSWORD="#GetSettings.Password#"
		CARDNUMBER="#crypt.value#"
		EXPIREDATE="#expiration#"
		CVV2=""
		AMOUNT="1"
		ORDER="U#attributes.UID#T#dateformat(now(),'yyyymmddhhmmss')#"
		CUSTOMER="#qry_get_user.user_ID#"
		FIRSTNAME="#FirstName#" 
		LASTNAME="#LastName#" 
		ADDRESS=""
		CITY=""
		STATE=""
		ZIP="#qry_get_user.cardzip#"
		COUNTRY=""
		PHONE=""	
		EMAIL=""
		ACTION="AUTH_ONLY"
		TEST="No">
	
		<cfif Results.response_code IS  "1">
			<cfset setactive = 1>
			<cfset adminnote = "#dateformat(now(),"mm/dd/yy")#: card authorized">
			<!--- Send through a void for this transaction --->
			<cfmodule template="../shopping/checkout/creditcards/authnetcapture30.cfm"
			QUERYNAME="VoidResults"
			LOGIN="#GetSettings.Username#"
	     	PASSWORD="#GetSettings.Password#"
	     	AMOUNT="1"
	     	TRANSACTNUM="#Results.trans_id#"
	     	EMAILMERCHANT="No"
	     	ACTION="VOID">			
			
		<cfelse>
			<cfset adminnote = "#dateformat(now(),"mm/dd/yy")#: card NOT authorized #Results.response_code# #Results.response_text#. ">
		</cfif>
		
	<cfelseif get_Order_Settings.CCProcess IS "Shift4OTN">

		<!--- $$$ ON THE NET --->	
		<cfset Vendor="CFWebstore6">
		<cfset custname=Trim(LastName)>
		<cfif Trim(FirstName) is not "">
			<cfset custname=custname&": "&Trim(FirstName)>
		</cfif>
		<cfset custname=Trim(custname&" (#Trim(qry_get_user.user_ID)#")>
	
		<cfparam name="attributes.CVV2" default="">
		<cfset s4CVV2Indicator="">
		<!--- commented out because the My Account page does not ask for the CVV2 information
		<cfif get_Order_Settings.usecvv2>
			<cfset s4CVV2Indicator="0">
		</cfif>
		--->
		<cfif attributes.CVV2 is not "">
			<cfset s4CVV2Indicator="1">
		</cfif>

		<cfinclude template="../shopping/checkout/creditcards/qry_get_Shift4OTN_Settings.cfm">
		<!--- auth request --->
		<cfmodule
			template="../shopping/checkout/creditcards/shift4otn.cfm"
			Result="OTN"
			URL="#get_Shift4OTN_Settings.URL#"
			ProxyServer="#get_Shift4OTN_Settings.ProxyServer#"
			ProxyPort="#get_Shift4OTN_Settings.ProxyPort#"
			ProxyUsername="#get_Shift4OTN_Settings.ProxyUsername#"
			ProxyPassword="#get_Shift4OTN_Settings.ProxyPassword#"
			SerialNumber = "#get_Shift4OTN_Settings.SerialNumber#"
			Username = "#get_Shift4OTN_Settings.Username#"
			Password = "#get_Shift4OTN_Settings.Password#"
			FunctionRequestCode = "1B"
			MerchantID = "#get_Shift4OTN_Settings.MID#"
			CustomerName="#custname#"
			CardNumber="#crypt.value#"
			ExpirationMonth="#Month(qry_get_user.cardexpire)#"
			ExpirationYear="#Year(qry_get_user.cardexpire)#"
			PrimaryAmount = "1.00"
			SecondaryAmount = "0.00"
			CVV2Indicator="#s4CVV2Indicator#"
			CVV2Code="#attributes.CVV2#"
			Notes = "Credit card test authorization to determine validity - to be voided."
			APIOptions="ALLDATA"
			Vendor="#Vendor#">

		<!--- void request --->
		<cfmodule
			template="../shopping/checkout/creditcards/shift4otn.cfm"
			Result = "OTNVoid"
			URL = "#get_Shift4OTN_Settings.URL#"
			ProxyServer="#get_Shift4OTN_Settings.ProxyServer#"
			ProxyPort="#get_Shift4OTN_Settings.ProxyPort#"
			ProxyUsername="#get_Shift4OTN_Settings.ProxyUsername#"
			ProxyPassword="#get_Shift4OTN_Settings.ProxyPassword#"
			SerialNumber = "#get_Shift4OTN_Settings.SerialNumber#"
			Username = "#get_Shift4OTN_Settings.Username#"
			Password = "#get_Shift4OTN_Settings.Password#"
			FunctionRequestCode = "08"
			MerchantID = "#get_Shift4OTN_Settings.MID#"
			Invoice = "#OTN.Invoice#"
			CardNumber = "#Right(OTN.CardNumber,4)#"
			TranID = "#OTN.TranID#"
			APIOptions="FULLVOID"
			Vendor="#Vendor#">

		<cfparam name="OTN.ValidAVS" default="">
		<cfparam name="OTN.CVV2Valid" default="">
		<cfif OTN.ErrorIndicator is "Y">
			<cfset adminnote = "#dateformat(now(),"mm/dd/yy")#: card NOT authorized - #OTN.LongError# (#OTN.PrimaryErrorCode#)">
		<cfelseif OTN.ResponseCode is not "A">
			<cfswitch expression="#OTN.ResponseCode#">
				<cfcase value="D"><cfset response="Declined"></cfcase>
				<cfcase value="F"><cfset response="AVS failure"></cfcase>
				<cfcase value="R"><cfset response="Voice referral"></cfcase>
				<cfcase value="X"><cfset response="Expired card"></cfcase>
				<cfdefaultcase><cfset response="Unknown response (#OTN.ResponseCode#)"></cfdefaultcase>
			</cfswitch>
			<cfset adminnote = "#dateformat(now(),"mm/dd/yy")#: card NOT authorized - #response#">
		<cfelseif OTN.ValidAVS is "N">
			<cfset adminnote = "#dateformat(now(),"mm/dd/yy")#: card NOT authorized - AVS failure">
		<cfelseif OTN.CVV2Valid is "N">
			<cfset adminnote = "#dateformat(now(),"mm/dd/yy")#: card NOT authorized - CVV2 failure">
		<cfelse>
			<cfset setactive = 1>
			<cfset adminnote = "#dateformat(now(),"mm/dd/yy")#: card authorized">
			<cfset token = OTN.UniqueID>
		</cfif>
	</cfif>
	
</cfif>
			
<cfif token is not "">
	<!--- When the "token" gets here, it should not have the '@' prefix which indicates a token. The update query below prepends the '@' --->
	<cfmodule template="../customtags/crypt.cfm" string="@#token#" key="#Request.encrypt_key#">
</cfif>

<!--- Update User Table --->
<cfquery name="UpdateUser" datasource="#Request.DS#" username="#Request.user#" password="#Request.pass#">
	UPDATE #Request.DB_Prefix#Users
	SET CardisValid = <cfqueryparam cfsqltype="#Request.SQL_Bit#" value="#setactive#">,
	<cfif token is not "">
		EncryptedCard = <cfqueryparam cfsqltype="CF_SQL_VARCHAR" value="#crypt.value#">,
	</cfif>
	AdminNotes = <cfqueryparam cfsqltype="CF_SQL_LONGVARCHAR" value="#adminnote# #qry_get_user.adminNotes#">,
	CurrentBalance = CurrentBalance + <cfqueryparam value="#amount#" cfsqltype="CF_SQL_DOUBLE">
	WHERE User_ID = <cfqueryparam value="#attributes.UID#" cfsqltype="CF_SQL_INTEGER">
</cfquery>
